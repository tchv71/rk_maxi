; Блок начальной настройки программируемого
; дешифратора
	.phase 0E360h
PROG_PAGE	EQU	0B500h
	jmp ON_DC
ON_DC:	MVI A,24H;  Включить режим начального программирования
	OUT -1;     внутренних устройств
	OUT -1;     Установить пзу в последнее странице FF00H—FFFFH 
	OUT 0E0h
	OUT 0E1h
	OUT 0E2h
	OUT 0E3h
	OUT 0E4h


	MVI A,10;    Номер линии выбора теневого озу
	OUT 0FEH;   Страницу FE00H—FEFFH перевести в режим озу
	MVI A,0A4H; Включить режим репрограммирования
	OUT -1;     внутренних устройств, повторяя младшие
	      ;     четыре разряда для последней страницы
	MVI	A,11
	OUT	0CEh
	MVI	A,80h
	STA	0CE00h

	MVI	A,10
	OUT	0B5h

	MVI C,ENDPRO-BEGPRO ; Скопировать в предпоследнюю
	LXI H,PROG_PAGE;      страницу, работающую как озу,
	LXI D,BEGPRO;         программу для полного программирования
	LDAX D;               всех внутренних устройств пэвм
	MOV M,A
	INX H
	INX D
	DCR C
	JNZ $-5 ;
	;LXI SP,0d800h;PROG_PAGE+0FFH; Установить указатель стека в открытой
	LXI H,PROG_PAGE+1 ; странице, записать в hl адрес операнда
		     ; команды out для обеспечения инкремента
		     ; начинаем с out 00
IF 0
	MVI B,80H    ; В - счетчик, А — номер линии выбора
	MVI A,5
	CALL PROG_PAGE  ; адреса 0000—7FFF — озу

	MVI B,40H    ; адреса 8000—ВFFF — теневое озу
	MVI A,10
	CALL PROG_PAGE

	MVI B,2H    ; дисплейный контроллер
	MVI A,2
	CALL PROG_PAGE  ; адреса C000 — C1FF - ВГ75

	MVI B,2      ; контроллер клавиатуры
	XRA A
	CALL PROG_PAGE  ; адреса C200-C3FF — ВВ55

	MVI B,2      ; контроллер периферийного оборудования
	INR A
	CALL PROG_PAGE  ; адреса С400—С5FF - ВВ55

	MVI B,2      ; контроллер прерываний
	MVI A,7
	CALL PROG_PAGE  ; адреса С600—С7FF - ВH59

	MVI B,2      ; таймер-2
	MVI A,8
	CALL PROG_PAGE  ; адреса С800—С9FF - ВИ53-2


	MVI B,2      ;
	MVI A,10
	CALL PROG_PAGE  ; адреса СA00—СBFF -  — теневое озу

	MVI B,2      ; таймер
	MVI A,6
	CALL PROG_PAGE  ; адреса СC00—СDFF - ВИ53

	MVI B,2      ; 
	MVI A,11
	CALL PROG_PAGE  ; адреса СE00—СFFF - ext byte

	MVI B,8H     ;
	MVI A,10     ;
	CALL PROG_PAGE  ; адреса D000 -D7FF

	MVI B,8H     ;
	MVI A,13     ;
	CALL PROG_PAGE  ; адреса D800 -DFFF


	MVI B,32-2   ; адреса E000-FDFF
	MVI A,4
	CALL PROG_PAGE  
ENDIF
	LXI D,MAP
LOOP:
	LDAX	D
	ORA	A
	JZ	END_LOOP
	MOV	B,A
	INX	D
	LDAX	D
	INX	D
	JMP	PROG_PAGE
END_LOOP:
	

	MVI A,0A4H  ; В режиме репрограммирования
	OUT -1      ; внутренних устройств перевести
	OUT -2      ; страницу FE00-FEFF в режим пзу
	MVI A,84H   ; Включить рабочий режим
	OUT -1
	MVI A,80H   ; Записать в системный регистр-начальные
	OUT -1      ; значения: турборежим выключен, нулевая
                    ; страница дополнительного озу
	lxi	h,0cc03h
	mvi	m,36h
	mvi	m,76h
	mvi	m,0b6h

	JMP 0F800h  ; Перейти к программированию контроллеров рк
BEGPRO:
	OUT 0
	INR M
	DCR B
	JZ LOOP
	JMP PROG_PAGE
ENDPRO:

MAP:	DB	80h,5,40h,10
	DB	2,2,2,0,2,1,2,7,2,8,2,10,2,6,2,11
	DB	8,10,8,13,30,4,0
	
	END














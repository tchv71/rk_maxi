; Блок начальной настройки программируемого
; дешифратора
ON_DC:	MVI A,24H;  Включить режим начального программирования
	OUT -1;     внутренних устройств
	OUT -1;     Установить пзу в последнее странице FF00H—FFFFH 
	MVI A,5;    Номер линии выбора основного озу
	OUT 0FEH;   Страницу FE00H—FEFFH перевести в режим озу
	MVI A,0A4H; Включить режим репрограммирования
	OUT -1;     внутренних устройств, повторяя младшие
	      ;     четыре разряда для последней страницы
	MVI C,ENDPRO-BEGPRO ; Скопировать в предпоследнюю
	LXI H,0FE00H;         страницу, работающую как озу,
	LXI D,BEGPRO;         программу для полного программирования
	LDAX D;               всех внутренних устройств пэвм
	MOV M,A
	INX H
	INX D
	DCR C
	JNZ $-5 ;
	LXI SP,0FEFFH; Установить указатель стека в открытой
	LXI H,0FE02H ; странице, записать в hl адрес операнда
		     ; команды out для обеспечения инкремента
		     ; начинаем с out 00
	MVI B,80H    ; В - счетчик, А — номер линии выбора
	MVI A,5
	CALL 0FE00H  ; адреса 0000—7FFF — озу
	MVI B,20H    ; контроллер клавиатуры
	XRA A
	CALL 0FE00H  ; адреса 8000-9FFF — D20
	MVI B,20H    ; контроллер периферийного оборудования
	INR A
	CALL 0FE00H  ; адреса A000—BFFF - D14
	MVI B,20H    ; дисплейный контроллер
	INR A
	CALL 0FE00H  ; адреса C000 — DFFF - D8
	MVI B,8H     ; контроллер пдп или младшая имс пзу
	INR A        ; контроллера нгмд
	CALL 0FE00H  ; адреса E000 -E7FF
	MVI B,8H     ; старшая имс пзу контроллера нгмд
	MVI A,7
	CALL 0FE00H  ; адреса E800-EFFF
	INR A; A=8     порт и регистр чтения данных контроллера
	OUT 0F0H     ; адреса F000-F0FF
	INR M        ; открыть озу в интервале F100-F7FF
	MVI A,5
	MVI B,7
	CALL 0FE00H
	MVI B,6H     ; пзу "системного монитора"
	MVI A,4
	CALL 0FE00H  ; адреса f800-ffff

; Программирование внешних устройств для обеспечения
; работы dos, так как операционная система нестандартно
; использует команды in и out
EQUP:	MVI A,0C4H  ; Включить режим репрограммирования
	OUT -1      ; внешних устройств
	MVI A,8
	MVI B,5
	MVI M,0F0H  ; Программирование портов контроллера нгмд
	CALL 0FE00H ; 0F0Н,0F1Н‚0F2Н,0F3Н и 0F4Н
	MVI A,2     ; Программирование порта контроллера
	OUT 0C1H    ; дисплея
	MVI A,0A4H  ; В режиме репрограммирования
	OUT -1      ; внутренних устройств перевести
	OUT -2      ; страницу FE00-FEFF в режим пзу
	MVI A,84H   ; Включить рабочий режим
	OUT -1
	MVI A,80H   ; Записать в системный регистр-начальные
	OUT-1       ; значения: турборежим выключен, нулевая
                    ; страница дополнительного озу
;	JMP BEGIN1  ; Перейти к программированию контроллеров рк
BEGPRO:
	OUT 0
	INR M
	DCR B
	RZ
	JMP 0FE00H
ENDPRO:

	
	END













